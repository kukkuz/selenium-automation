plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'checkstyle'
}

group 'com.test'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation group: 'org.testng', name: 'testng', version: '7.1.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation "com.aventstack:extentreports-testng-adapter:1.0.7"
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    systemProperties System.properties
    useTestNG {
        suites 'src/test/resources/testng.yaml'
        useDefaultListeners = false // switch off testNG reports
    }
    testLogging {
        showStandardStreams = true
        events "started", "passed", "failed", "skipped"
        exceptionFormat = "short"
    }
    // switch off default gradle reports
    reports {
        junitXml.enabled = false
        html.enabled = false
    }
}

// for tasks checkstyleMain, checkstyleTest, checkstyleSourceSet
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

// extension to configure checkstyle plugin
checkstyle {
    toolVersion '8.31'
    configFile rootProject.file('checkstyle.xml')
    ignoreFailures true
    showViolations true
}
